package funkin.ui.mainmenu;

import funkin.assets.FunkinAssetCache;
import flixel.addons.transition.FlxTransitionableState;
#if FEATURE_DEBUG_MENU
import funkin.ui.debug.DebugMenuSubState;
#end
import flixel.FlxObject;
import flixel.FlxSubState;
import flixel.FlxSprite;
import flixel.effects.FlxFlicker;
import flixel.math.FlxPoint;
import flixel.util.typeLimit.NextState;
import flixel.util.FlxColor;
import flixel.tweens.FlxEase;
import flixel.text.FlxText.FlxTextFormat;
import funkin.graphics.FunkinCamera;
import funkin.audio.FunkinSound;
import funkin.util.SwipeUtil;
import funkin.util.InputUtil;
import flixel.tweens.FlxTween;
import funkin.ui.MusicBeatState;
import funkin.ui.UIStateMachine;
import funkin.ui.UIStateMachine.UIState;
import flixel.util.FlxTimer;
import funkin.ui.AtlasMenuList.AtlasMenuItem;
import funkin.ui.freeplay.FreeplayState;
import funkin.ui.MenuList.MenuTypedList;
import funkin.ui.MenuList.MenuListItem;
import funkin.ui.title.TitleState;
import funkin.ui.story.StoryMenuState;
import funkin.ui.Prompt;
import funkin.util.WindowUtil;
import funkin.mobile.ui.FunkinButton;
import funkin.util.MathUtil;
import funkin.util.TouchUtil;
import funkin.api.newgrounds.Referral;
import funkin.ui.mainmenu.UpgradeSparkle;
import flixel.group.FlxSpriteGroup.FlxTypedSpriteGroup;
import funkin.ui.quickpanel.QuickPanelState;
#if FEATURE_DISCORD_RPC
import funkin.api.discord.DiscordClient;
#end
#if FEATURE_NEWGROUNDS
import funkin.api.newgrounds.NewgroundsClient;
#end
#if mobile
import funkin.mobile.input.ControlsHandler;
import funkin.mobile.util.InAppPurchasesUtil;
#end
import funkin.assets.Paths as Paths;
import funkin.assets.Assets as Assets;

@:nullSafety
class MainMenuState extends MusicBeatState
{
  static inline final BUILD_VERSION:String = "v0.8.5.1";
  static inline final BUILD_NUMBER:String = "67";
  static inline final TAGGED_BUILD_NUMBER:String = "67";

  var menuItems:Null<MenuTypedList<AtlasMenuItem>>;
  var bg:Null<FlxSprite>;
  var magenta:FlxSprite;
  var camFollow:FlxObject;
  #if mobile
  var gyroPan:Null<FlxPoint>;
  #end
  var overrideMusic:Bool = false;
  var uiStateMachine:UIStateMachine = new UIStateMachine();
  var canInteract(get, never):Bool;

  function get_canInteract():Bool
  {
    return uiStateMachine.canInteract();
  }

  static var rememberedSelectedIndex:Int = 0;

  var hasUpgraded:Bool = false;
  var upgradeSparkles:FlxTypedSpriteGroup<UpgradeSparkle>;

  public function new(_overrideMusic:Bool = false)
  {
    super();
    overrideMusic = _overrideMusic;

    uiStateMachine.transition(EnteringMainMenu);

    upgradeSparkles = new FlxTypedSpriteGroup<UpgradeSparkle>();
    magenta = new FlxSprite(Paths.image('ui/main-menu/menu-bg-magenta').toFlxGraphicAsset());
    camFollow = new FlxObject(0, 0, 1, 1);

    FlxG.keys.enabled = true;
  }

  override function create():Void
  {
    #if FEATURE_DISCORD_RPC
    DiscordClient.instance.setPresence({state: "In the Menus", details: null});
    #end

    FlxG.cameras.reset(new FunkinCamera('mainMenu'));

    transIn = FlxTransitionableState.defaultTransIn;
    transOut = FlxTransitionableState.defaultTransOut;

    #if FEATURE_MOBILE_IAP
    if (InAppPurchasesUtil.hasInitialized) Preferences.noAds = InAppPurchasesUtil.isPurchased(InAppPurchasesUtil.UPGRADE_PRODUCT_ID);
    hasUpgraded = Preferences.noAds;
    #else
    hasUpgraded = true;
    #end

    if (!overrideMusic) playMenuMusic();

    persistentUpdate = true;
    persistentDraw = true;

    bg = new FlxSprite(Paths.image('ui/main-menu/menu-bg').toFlxGraphicAsset());
    bg.scrollFactor.x = #if !mobile 0 #else 0.17 #end;
    bg.scrollFactor.y = 0.17;
    bg.setGraphicSize(Std.int(FlxG.width * 1.2));
    bg.updateHitbox();
    bg.screenCenter();
    add(bg);

    add(camFollow);

    magenta.scrollFactor.copyFrom(bg.scrollFactor);
    magenta.setGraphicSize(Std.int(bg.width));
    magenta.updateHitbox();
    magenta.x = bg.x;
    magenta.y = bg.y;
    magenta.visible = false;

    if (Preferences.flashingLights) add(magenta);

    menuItems = new MenuTypedList<AtlasMenuItem>();
    add(menuItems);

    menuItems.onChange.add(onMenuItemChange);
    menuItems.onAcceptPress.add(_ ->
    {
      FlxFlicker.flicker(magenta, 1.1, 0.15, false, true);
      uiStateMachine.transition(Interacting);
    });

    menuItems.enabled = true;
    createMenuItem('storymode', 'ui/main-menu/items/story-mode', () ->
    {
      FlxG.signals.preStateSwitch.addOnce(() ->
      {
        FunkinAssetCache.instance.preparePurgeCache();
      });

      FlxG.signals.postStateSwitch.addOnce(() ->
      {
        funkin.memory.FunkinMemory.clearFreeplay();
        FunkinAssetCache.instance.purgeCache(true);
      });
      startExitState(() -> new StoryMenuState());
    });
    createMenuItem('freeplay', 'ui/main-menu/items/freeplay', () ->
    {
      persistentDraw = true;
      persistentUpdate = false;
      rememberedSelectedIndex = menuItems?.selectedIndex ?? 0;
      FlxTransitionableState.skipNextTransIn = true;
      FlxTransitionableState.skipNextTransOut = true;

      FreeplayState.CUTOUT_WIDTH = funkin.ui.FullScreenScaleMode.gameCutoutSize.x / 1.5;

      #if FEATURE_DEBUG_FUNCTIONS
      var targetCharacter:Null<String> = FlxG.keys.pressed.SHIFT ? (FreeplayState.rememberedCharacterId == "pico" ? "bf" : "pico") : FreeplayState.rememberedCharacterId;
      #else
      var targetCharacter:Null<String> = FreeplayState.rememberedCharacterId;
      #end

      if (!hasUpgraded)
      {
        for (i in 0...upgradeSparkles.length)
        {
          upgradeSparkles.members[i].cancelSparkle();
        }
      }

      openSubState(new FreeplayState({
        character: targetCharacter
      }));
    });

    if (hasUpgraded)
    {
      #if FEATURE_OPEN_URL
      var hasPopupBlocker = #if web true #else false #end;
      createMenuItem('merch', 'ui/main-menu/items/merch', selectMerch, hasPopupBlocker);
      #end
    }
    else
    {
      add(upgradeSparkles);

      createMenuItem('upgrade', 'ui/main-menu/items/upgrade', function()
      {
        #if FEATURE_MOBILE_IAP
        InAppPurchasesUtil.purchase(InAppPurchasesUtil.UPGRADE_PRODUCT_ID, FlxG.resetState);
        uiStateMachine.transition(Idle);
        #end
      });
    }

    if (#if mobile ControlsHandler.usingExternalInputDevice #else true #end)
    {
      createMenuItem('options', 'ui/main-menu/items/options', function()
      {
        startExitState(() -> new funkin.ui.options.OptionsState());
      });
    }

    createMenuItem('credits', 'ui/main-menu/items/credits', function()
    {
      startExitState(() -> new funkin.ui.credits.CreditsState());
    });

    final spacing:Float = 160;
    final top:Float = (FlxG.height - (spacing * (menuItems.length - 1))) / 2;

    for (index => menuItem in menuItems)
    {
      menuItem.x = FlxG.width / 2;
      menuItem.y = top + spacing * index;
      menuItem.scrollFactor.x = #if !mobile 0.0 #else 0.4 #end;
      menuItem.scrollFactor.y = 0.4;

      if (index == 1) camFollow.setPosition(menuItem.getGraphicMidpoint().x, menuItem.getGraphicMidpoint().y);
    }

    menuItems.selectItem(rememberedSelectedIndex);

    if (!hasUpgraded)
    {
      var targetItem = menuItems.members[2];
      for (_ in 0...8)
      {
        var sparkle:UpgradeSparkle = new UpgradeSparkle(
          targetItem.x - (targetItem.width / 2),
          targetItem.y - (targetItem.height / 2),
          targetItem.width,
          targetItem.height,
          FlxG.random.bool(80)
        );
        upgradeSparkles.add(sparkle);

        sparkle.scrollFactor.x = 0.0;
        sparkle.scrollFactor.y = 0.4;
      }

      subStateClosed.add(_ ->
      {
        for (i in 0...upgradeSparkles.length)
        {
          upgradeSparkles.members[i].restartSparkle();
        }
      });
    }

    resetCamStuff();

    subStateClosed.add(_ -> resetCamStuff(false));

    subStateOpened.add((sub:FlxSubState) ->
    {
      if (Std.isOfType(sub, FreeplayState))
      {
        FlxTimer.wait(0.5, () ->
        {
          magenta.visible = false;
        });
      }
    });

    #if mobile
    gyroPan = new FlxPoint();

    camFollow.y = bg.getGraphicMidpoint().y;

    addBackButton(FlxG.width - 230, FlxG.height - 200, FlxColor.WHITE, goBack, 1.0);

    if (!ControlsHandler.usingExternalInputDevice)
    {
      addOptionsButton(35, FlxG.height - 210, goOptions);
    }

    backButton?.onConfirmStart.add(() ->
    {
      uiStateMachine.transition(Interacting);
    });

    optionsButton?.onConfirmStart.add(() ->
    {
      uiStateMachine.transition(Interacting);
    });
    #end

    super.create();

    initLeftWatermarkText();
  }

  function initLeftWatermarkText():Void
  {
    if (leftWatermarkText == null) return;

    final prefix:String = '${BUILD_VERSION} - Build: ';

    leftWatermarkText.text = prefix + BUILD_NUMBER;

    #if FEATURE_NEWGROUNDS
    if (NewgroundsClient.instance.isLoggedIn())
    {
      leftWatermarkText.text += ' | Newgrounds: Logged in as ${NewgroundsClient.instance.user?.name}';
    }
    #end

    if (BUILD_NUMBER == TAGGED_BUILD_NUMBER)
    {
      final startIndex:Int = prefix.length;
      final endIndex:Int = startIndex + BUILD_NUMBER.length;

      FlxTween.num(0, 1, 0.5, {type: PINGPONG, ease: FlxEase.quadInOut}, (value:Float) ->
      {
        if (leftWatermarkText == null) return;

        final blendedColor:FlxColor = FlxColor.interpolate(FlxColor.YELLOW, FlxColor.WHITE, value);

        leftWatermarkText.addFormat(new FlxTextFormat(blendedColor), startIndex, endIndex);
      });
    }

    if (rightWatermarkText == null) return;

    rightWatermarkText.text += 'Mod Menu Playtest Build (v0.9 Feature Preview #3)';
  }

  function playMenuMusic():Void
  {
    FunkinSound.playMusic('ui/main-menu/freaky-menu/freaky-menu', {
      overrideExisting: true,
      restartTrack: false,
      persist: true
    });
  }

  function resetCamStuff(snap:Bool = true):Void
  {
    FlxG.camera.follow(camFollow, null, 0.06);

    if (snap) FlxG.camera.snapToTarget();
  }

  function createMenuItem(name:String, atlas:String, callback:Void->Void, fireInstantly:Bool = false):Void
  {
    if (menuItems == null) return;

    var item:AtlasMenuItem = new AtlasMenuItem(name, Assets.getSparrowAtlas(Paths.image(atlas)), callback);
    item.fireInstantly = fireInstantly;
    item.ID = menuItems.length;
    item.scrollFactor.set();

    item.centered = true;
    item.changeAnim('idle');
    menuItems.addItem(name, item);
  }

  var buttonGrp:Array<FlxSprite> = [];

  function createMenuButtion(name:String, atlas:String, callback:Void->Void):Void
  {
    var item:FunkinButton = new FunkinButton(Math.round(FlxG.width * 0.8), Math.round(FlxG.height * 0.7));
    item.makeGraphic(250, 250, FlxColor.BLUE);
    item.onDown.add(callback);
    buttonGrp.push(item);
  }

  override function closeSubState():Void
  {
    magenta.visible = false;

    if (!(subState is flixel.addons.transition.Transition))
    {
      uiStateMachine.transition(Idle);

      #if FEATURE_TOUCH_CONTROLS
      backButton?.animation.play('idle');
      backButton?.resetCallbacks();

      optionsButton?.animation.play('idle');
      optionsButton?.resetCallbacks();
      #end
    }

    super.closeSubState();
  }

  function onMenuItemChange(selected:MenuListItem)
  {
    if (#if mobile ControlsHandler.usingExternalInputDevice #else true #end) camFollow.setPosition(
      selected.getGraphicMidpoint().x,
      selected.getGraphicMidpoint().y
    );
  }

  #if FEATURE_OPEN_URL
  function selectDonate()
  {
    WindowUtil.openURL(Constants.URL_ITCH);
  }

  function selectMerch()
  {
    Referral.doMerchReferral();
    uiStateMachine.transition(Idle);
  }
  #end

  public function openPrompt(prompt:Prompt, onClose:Void->Void):Void
  {
    uiStateMachine.transition(Interacting);
    persistentUpdate = false;

    prompt.closeCallback = function()
    {
      if (onClose != null) onClose();
    }

    openSubState(prompt);
  }

  function startExitState(state:NextState):Void
  {
    if (menuItems == null) return;

    uiStateMachine.transition(Exiting);
    rememberedSelectedIndex = menuItems.selectedIndex;

    var fadeOutDuration:Float = 0.4;
    menuItems.forEach(item ->
    {
      if (rememberedSelectedIndex != item.ID) FlxTween.tween(item, {alpha: 0}, fadeOutDuration, {ease: FlxEase.quadOut});
      else
        item.visible = false;
    });

    #if mobile
    if (optionsButton != null) FlxTween.tween(optionsButton, {alpha: 0}, fadeOutDuration, {ease: FlxEase.quadOut});
    if (backButton != null) FlxTween.tween(backButton, {alpha: 0}, fadeOutDuration, {ease: FlxEase.quadOut});
    #end

    FlxTimer.wait(fadeOutDuration, () ->
    {
      FlxG.switchState(state);
    });
  }

  override function update(elapsed:Float):Void
  {
    super.update(elapsed);

    Conductor.instance.update();

    #if mobile
    if (gyroPan != null && bg != null && !ControlsHandler.usingExternalInputDevice)
    {
      gyroPan.add(FlxG.gyroscope.pitch * -1.25, FlxG.gyroscope.roll * -1.25);

      gyroPan.x = MathUtil.smoothLerpPrecision(gyroPan.x, 0, elapsed, 2.5);
      gyroPan.y = MathUtil.smoothLerpPrecision(gyroPan.y, 0, elapsed, 2.5);

      camFollow.x = bg.getGraphicMidpoint().x - gyroPan.x;
      camFollow.y = bg.getGraphicMidpoint().y - gyroPan.y;
    }
    #end

    if ((FlxG.sound.music?.volume ?? 1.0) < 0.8)
    {
      FlxG.sound.music.volume += 0.5 * elapsed;
    }
    handleInputs();

    if (menuItems != null) menuItems.busy = !canInteract;

    #if mobile
    if (optionsButton != null)
    {
      optionsButton.active = canInteract || optionsButton.confirming;
      optionsButton.enabled = optionsButton.active;
    }
    if (backButton != null)
    {
      backButton.active = canInteract || backButton.confirming;
      backButton.enabled = backButton.active;
    }
    #end
  }

  function emptyTallies():Dynamic
  {
    return {
      sick: 0,
      good: 0,
      bad: 0,
      shit: 0,
      missed: 0,
      combo: 0,
      maxCombo: 0,
      totalNotesHit: 0,
      totalNotes: 0,
    };
  }

  function handleInputs():Void
  {
    if (!canInteract) return;

    #if FEATURE_DEBUG_MENU
    if (controls.DEBUG_MENU)
    {
      persistentUpdate = false;
      uiStateMachine.transition(Interacting);

      if (menuItems != null && menuItems.busy) menuItems.cancelAccept();

      FlxG.state.openSubState(new DebugMenuSubState());
    }
    #end

    #if FEATURE_DEBUG_FUNCTIONS
    if (InputUtil.allPressedWithDebounce([CONTROL, ALT, SHIFT, P]))
    {
      FlxG.switchState(() -> new funkin.ui.charSelect.CharacterUnlockState('pico'));
    }

    if (InputUtil.allPressedWithDebounce([CONTROL, ALT, SHIFT, W]))
    {
      FunkinSound.playOnce(Paths.sound('ui/main-menu/confirm-menu').toFlxSoundAsset());
      funkin.save.Save.instance.setLevelScore('weekend1', 'easy', {
        score: 1,
        tallies: emptyTallies()
      });
    }

    if (InputUtil.allPressedWithDebounce([CONTROL, ALT, SHIFT, M]))
    {
      FunkinSound.playOnce(Paths.sound('ui/main-menu/confirm-menu').toFlxSoundAsset());
      for (diff in ['easy', 'normal', 'hard'])
      {
        funkin.save.Save.instance.setLevelScore('weekend1', diff, {
          score: 0,
          tallies: emptyTallies()
        });
      }
    }

    if (InputUtil.allPressedWithDebounce([CONTROL, ALT, SHIFT, R]))
    {
      funkin.save.Save.instance.setSongScore('tutorial', 'easy', {
        score: 1234567,
        tallies: {
          sick: 0,
          good: 0,
          bad: 0,
          shit: 1,
          missed: 0,
          combo: 0,
          maxCombo: 0,
          totalNotesHit: 1,
          totalNotes: 10,
        }
      });
    }

    if (InputUtil.allPressedWithDebounce([CONTROL, ALT, SHIFT, N]))
    {
      @:privateAccess
      {
        funkin.save.Save.instance.data.unlocks.charactersSeen = ["bf"];
        funkin.save.Save.instance.oldChar.value = false;
      }
    }

    if (InputUtil.allPressedWithDebounce([CONTROL, ALT, SHIFT, E]))
    {
      funkin.save.Save.instance.debug_dumpSaveJsonSave();
    }
    #end

    if (controls.BACK_P) goBack();
  }

  function goOptions():Void
  {
    startExitState(() -> new funkin.ui.options.OptionsState());
  }

  function goBack():Void
  {
    uiStateMachine.transition(Exiting);
    rememberedSelectedIndex = menuItems?.selectedIndex ?? 0;
    FunkinSound.playOnce(Paths.sound('ui/main-menu/cancel-menu').toString());

    FlxG.switchState(() -> new TitleState());
  }
}
